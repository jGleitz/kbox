version = "0.12.1"
group = 'ch.tutteli.kbox'

buildscript {
    ext {
        atrium_version = '0.8.0-beta'
        kotlin_version = '1.2.50'
        mockito_kotlin_version = '1.5.0'

        // detekt/sonarcloud
        detekt_version = '1.0.0-RC14'
    }

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "ch.tutteli:tutteli-gradle-dokka:${gradle.ext.tutteli_plugins_version}"
        classpath("ch.tutteli:tutteli-gradle-kotlin-module-info:$gradle.ext.tutteli_plugins_version")
        classpath "ch.tutteli:tutteli-gradle-kotlin-utils:${gradle.ext.tutteli_plugins_version}"
        classpath "ch.tutteli:tutteli-gradle-project-utils:${gradle.ext.tutteli_plugins_version}"
        classpath "ch.tutteli:tutteli-gradle-publish:${gradle.ext.tutteli_plugins_version}"
        classpath "ch.tutteli:tutteli-gradle-spek:${gradle.ext.tutteli_plugins_version}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

apply plugin: 'ch.tutteli.project.utils'
apply plugin: 'ch.tutteli.kotlin.utils'
kotlinutils.kotlinVersion = kotlin_version

repositories {
    mavenCentral()
    jcenter()
}
apply plugin: "io.gitlab.arturbosch.detekt"
detekt {
    version = detekt_version
    failFast = true
    input = files(rootProject.projectDir)
    filters = ".*/resources/.*,.*$rootProject.name/.*/build/.*"
    config = files("$rootProject.projectDir/gradle/scripts/detekt.yml")
    reports {
        xml.enabled = true
    }
}

subprojects {
    repositories { jcenter() }
    description = 'A utility library for Kotlin'
}

configureCommonProjects()
configureJsProjects()
configureJvmProjects()
configureAndroidProjects()

def publishedProjects = subprojects

configure(getCommonProjects()){
    compileKotlinCommon {
        kotlinOptions.allWarningsAsErrors = true
    }
    compileTestKotlinCommon{
        kotlinOptions.allWarningsAsErrors = true
    }
}
configure(getJsProjects()) {
    compileKotlin2Js {
        kotlinOptions.allWarningsAsErrors = true
    }
    compileTestKotlin2Js{
        kotlinOptions.allWarningsAsErrors = true
    }
}

configure(getJvmProjects()){
    apply plugin: 'ch.tutteli.kotlin.module.info'
    compileKotlin {
        kotlinOptions.allWarningsAsErrors = true
    }
    compileTestKotlin{
        kotlinOptions.allWarningsAsErrors = true
    }
}

def jvmLikeProjects = getJvmProjects() + getAndroidProjects()
configure(jvmLikeProjects) { subproject ->
    apply plugin: 'ch.tutteli.spek'

    dependencies {
        testCompile "ch.tutteli.atrium:atrium-cc-en_GB-robstoll:$atrium_version", excluding {
            kotlin()
            kbox()
        }
        testCompile "ch.tutteli.atrium:atrium-spec:$atrium_version", excluding {
            kotlin()
            kbox()
            exclude group: 'ch.tutteli.atrium'
        }
        testCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_version", excludeKotlin
    }

    apply plugin: 'ch.tutteli.dokka'
}


// has to be after the above blocks which define `it.ext.publish_artifacts`
configure(publishedProjects) { subproject ->
    apply plugin: 'ch.tutteli.publish'
}

/*

Release & deploy a commit
--------------------------------
1. search for X.Y.Z-SNAPSHOT and replace with X.Y.Z
2. update master:
    a) point to the tag
        1) search for `branch=master` and replace it with `tag=vX.Y.Z` (build status and coverage in README.md)
        2) search for `tree/master` and replace it with `tree/vX.Y.Z` (README.md)
    b) commit (modified .travis.yml, build.gradle, README.md)
    c) git tag vX.Y.Z
    d) git push origin vX.Y.Z
4. deploy to bintray:
    a) gr clean publishToBintray
    b) Log in to bintray, check and publish new jars
5. create release on github

Prepare next dev cycle
-----------------------
1. point to master
   a) search for `tag=vX.Y.Z` and replace it with `branch=master`
   b) search for `tree/vX.Y.Z` and replace it with `tree/master`
2. search for X.Y.Z and replace with X.Y.Z-SNAPSHOT
3. commit & push changes

*/

